// Copyright 2019 Google LLC
// Copyright 2019-present Open Networking Foundation
// SPDX-License-Identifier: Apache-2.0
scenario SubscribeAdminState(ifname: string) version 1.0.0 {
  gnmi := gNMI.Subscribe
  gnmi << SubscribeRequest {
         subscribe {
             mode: ONCE
             subscription [ {
                 path: /interfaces/interface[name=$ifname]/state/admin-status } ] } }
  gnmi >> SubscribeResponse {
         update {
           update [ {
             path: /interfaces/interface[name=$ifname]/state/admin-status
             val { string_val: _ } } ] } }
  gnmi >> SubscribeResponse { sync_response: true }
  close gnmi
}

scenario SubscribeAdminStateOnFirstInterface() version 1.0.0 {
  gnmi := gNMI.Subscribe
  gnmi << SubscribeRequest {
         subscribe {
             mode: ONCE
             subscription [ { path: /interfaces/interface[name=*]/state/ifindex } ] } }
  gnmi >> SubscribeResponse {
         update {
           update [ {
             path: /interfaces/interface[$ifname:=name]/state/ifindex
             val { int_val: _ } } ] } }
  execute SubscribeAdminState( $ifname )
  ZeroOrMore {
    gnmi >> SubscribeResponse { update { update [ { path: _ val { int_val: _ } } ] } }
  }
  gnmi >> SubscribeResponse { sync_response: true }
  close gnmi
}
