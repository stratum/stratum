# Copyright 2018 Google LLC
# Copyright 2018-present Open Networking Foundation
# SPDX-License-Identifier: Apache-2.0

load("@rules_pkg//:pkg.bzl", "pkg_deb", "pkg_tar")
load("//bazel:rules.bzl", "STRATUM_DEFAULT_COPTS")

licenses(["notice"])  # Apache v2

package(
    default_visibility = ["//visibility:public"],
)

cc_library(
    name = "bf_interface",
    srcs = ["bf_interface.cc"],
    hdrs = ["bf_interface.h"],
    copts = STRATUM_DEFAULT_COPTS,
    deps = [
        "//stratum/glue:init_google",
        "//stratum/glue:logging",
        "//stratum/hal/lib/barefoot:bf_init",
        "//stratum/hal/lib/barefoot:bf_sde_wrapper",
        "//stratum/hal/lib/barefoot:bfrt_action_profile_manager",
        "//stratum/hal/lib/barefoot:bfrt_counter_manager",
        "//stratum/hal/lib/barefoot:bfrt_node",
        "//stratum/hal/lib/barefoot:bfrt_pre_manager",
        "//stratum/hal/lib/barefoot:bfrt_table_manager",
        "@com_github_google_glog//:glog",
        "@com_github_p4lang_p4runtime//:p4runtime_cc_proto",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/strings",
        "@local_barefoot_bin//:bfsde",
    ],
    alwayslink = True,
)

cc_binary(
    name = "libstratum_bfrt.so",
    copts = STRATUM_DEFAULT_COPTS,
    deps = [":bf_interface"],
)

cc_binary(
    name = "test_c",
    srcs = ["test.c"],
    deps = [
        ":bf_interface",
    ],
)

cc_binary(
    name = "test_cc",
    srcs = ["test.cc"],
    deps = [
        ":bf_interface",
        "@com_github_p4lang_p4runtime//:p4runtime_cc_proto",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/synchronization",
    ],
)

# Rules for Debian package
pkg_tar(
    name = "stratum_bfrt_lib",
    srcs = [
        ":libstratum_bfrt.so",
    ],
    mode = "0644",
    package_dir = "/usr/lib",
    symlinks = {
        "./usr/lib/libstratum_bfrt.so.0": "libstratum_bfrt.so",
        "./usr/lib/libstratum_bfrt.so.0.0.0": "libstratum_bfrt.so",
    },
)

# TODO(bocon): need headers

pkg_tar(
    name = "stratum_bin_tools",
    srcs = [
        "//stratum/hal/bin/barefoot:bf_pipeline_builder",
    ],
    mode = "0755",
    package_dir = "/usr/bin",
)

pkg_tar(
    name = "stratum_bfrt_lib_data",
    extension = "tar.bz2",
    deps = [
        ":stratum_bfrt_lib",
        ":stratum_bin_tools",
        "//stratum/hal/bin/barefoot:stratum_shareable_files",
        "@local_barefoot_bin//:bf_library_files",
        "@local_barefoot_bin//:bf_shareable_files",
        "@local_barefoot_bin//:kernel_module",
    ],
)

pkg_deb(
    name = "stratum_bfrt_lib_deb",
    architecture = "amd64",
    conflicts = [
        "stratum-bf",
        "stratum-bfrt",
    ],
    data = ":stratum_bfrt_lib_data",
    depends = [
        "kmod",
        "libboost-thread1.62.0",
        "libbz2-1.0",  # is this needed?
        "libedit2",
        "libjudydebian1",
        "libssl1.1",
    ],
    description = "The Stratum package for Barefoot Tofino-based platform",
    homepage = "https://stratumproject.org/",
    maintainer = "The Stratum Project",
    package = "stratum-bfrt-lib-dev",
    # postinst = "deb/postinst",
    # prerm = "deb/prerm",
    version = "0.0.1",
)
